config {
    type: "table",
    name: "order_items_output_post",
    schema: dataform.projectConfig.vars.orders_output_dataset_post,
    description: "An Orders Output table, Alternative",
    tags: ["output"],

    bigquery: {
        labels: {
            data_level: variables.output_data_level,
            data_domain: "orders"
        }
    },

    columns: {
        order_id: docs.order_id,
        user_id: docs.user_id,
        product_id : docs.product_id ,
        status: docs.status,
        created_at: docs.created_at,
        shipped_at: docs.shipped_at,
        delivered_at: docs.delivered_at,
        returned_at: docs.returned_at,
        sale_price: docs.sale_price,
        first_name: docs.first_name,
        last_name: docs.last_name,
        email: docs.email,
        age: docs.age,
        gender: docs.gender,
        state: docs.state,
        street_address: docs.street_address,
        postal_code: docs.postal_code,
        city: docs.city,
        country: docs.country,
        user_latitude: docs.user_latitude,
        user_longitude: docs.user_longitude,
        traffic_source: docs.traffic_source,
        user_created_at: docs.user_created_at,
        lifetime_orders: docs.lifetime_orders,
        lifetime_revenue: docs.lifetime_revenue,
        first_order: docs.first_order,
        latest_order: docs.latest_order,
        number_of_distinct_months_with_orders: docs.number_of_distinct_months_with_orders,
        product_brand: docs.product_brand,
        product_category: docs.product_category,
        product_name: docs.product_name,
        product_department: docs.product_department,
        distribtution_center_name: docs.distribtution_center_name,
        distribution_center_latitude: docs.distribution_center_latitude,
        distribution_center_longitude: docs.distribution_center_longitude,
        order_sequence_number: docs.order_sequence_number,
        is_first_purchase: docs.is_first_purchase,
        order_amount: docs.order_amount,
        order_cost: docs.order_cost,
        order_margin: docs.order_margin,
    },
}

WITH user_order_facts as (
    SELECT
        user_id
        , COUNT(DISTINCT order_id) AS lifetime_orders
        , SUM(sale_price) AS lifetime_revenue
        , CAST(MIN(created_at)  AS TIMESTAMP) AS first_order
        , CAST(MAX(created_at)  AS TIMESTAMP)  AS latest_order
        , COUNT(DISTINCT FORMAT_TIMESTAMP('%Y%m', created_at))  AS number_of_distinct_months_with_orders
      FROM ${ref("order_items_intermediate")}
      GROUP BY 1
)

SELECT
oi.order_id,
oi.user_id,
oi.product_id,
-- oi.inventory_item_id,
oi.status,
oi.created_at,
oi.shipped_at,
oi.delivered_at,
oi.returned_at,
oi.sale_price,

u.first_name,
u.last_name,
u.email,
u.age,
u.gender,
u.state,
u.street_address,
u.postal_code,
u.city,
u.country,
u.latitude as user_latitude,
u.longitude as user_longitude,
u.traffic_source,
u.created_at as user_created_at,

uof.lifetime_orders,
uof.lifetime_revenue,
uof.first_order,
uof.latest_order,
uof.number_of_distinct_months_with_orders,

p.category as product_category,
p.name as product_name,
p.brand as product_brand,
p.department as product_department,

dc.name as distribtution_center_name,
dc.latitude as distribution_center_latitude,
dc.longitude as distribution_center_longitude,


RANK() OVER (PARTITION BY oi.user_id, oi.order_id ORDER BY oi.created_at) as order_sequence_number,
CASE WHEN RANK() OVER (PARTITION BY oi.user_id ORDER BY oi.created_at) = 1 THEN true ELSE false END as is_first_purchase,
SUM(oi.sale_price) OVER (PARTITION BY oi.order_id) as order_amount,
SUM(ii.cost) OVER (PARTITION BY oi.order_id) as order_cost,
(SUM(sale_price) OVER (PARTITION BY oi.order_id) - SUM(ii.cost) OVER (PARTITION BY oi.order_id)) as order_margin
FROM ${ref("order_items_intermediate")} oi
JOIN ${ref("inventory_items_source")} ii ON oi.inventory_item_id = ii.id
JOIN ${ref("users_source")} u ON oi.user_id = u.id
JOIN user_order_facts uof ON oi.user_id = uof.user_id
JOIN ${ref("products_source")} p ON ii.product_id = p.id
JOIN ${ref("distribution_centers_source")} dc ON p.distribution_center_id = dc.id


post_operations {
    SELECT ${dataform.projectConfig.vars.resources_dataset}.remote_table_tagging(
        "${self()}",
        "marc@google.com",
        "${variables.output_data_level}",
        "${variables.orders_data_domain}"
        );
}
