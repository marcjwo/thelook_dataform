config {
    type: "view",
    name: "sessions_intermediate",
    schema: dataform.projectConfig.vars.web_intermediate_dataset,
    description: "Sessions Intermediate",
    tags: ["intermediate"],

    bigquery: {
        labels: {
            data_domain: "web",
            data_level: variables.intermediate_data_level,
        }
    },
}

SELECT
session_id
, CAST(MIN(created_at) AS TIMESTAMP) AS session_start
, CAST(MAX(created_at) AS TIMESTAMP) AS session_end
, COUNT(*) AS number_of_events_in_session
, SUM(CASE WHEN event_type IN ('category','brand') THEN 1 ELSE NULL END) AS browse_events
, SUM(CASE WHEN event_type = 'product' THEN 1 ELSE NULL END) AS product_events
, SUM(CASE WHEN event_type = 'cart' THEN 1 ELSE NULL END) AS cart_events
, SUM(CASE WHEN event_type = 'purchase' THEN 1 ELSE NULL end) AS purchase_events
, CAST(MAX(user_id) AS INT64)  AS session_user_id
, MIN(id) AS landing_event_id
, MAX(id) AS bounce_event_id
FROM ${ref("events_source")}
GROUP BY session_id